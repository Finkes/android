apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 27
    buildToolsVersion '28.0.2'
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "com.mathandoro.coachplus"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 9
        versionName "1.1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [appName:"Coach+"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions 'tier'
    productFlavors {
        dev {
            dimension 'tier'
            manifestPlaceholders = [appName:"Coach+ (Dev)"]
            applicationId "com.mathandoro.coachplus.dev"
            buildConfigField "String", "BASE_URL", '"https://dev.coach.plus"'
        }
        beta {
            dimension 'tier'
            manifestPlaceholders = [appName:"Coach+ (Beta)"]
            applicationId "com.mathandoro.coachplus.beta"
            buildConfigField "String", "BASE_URL", '"https://dev.coach.plus"'
        }
        prod {
            dimension 'tier'
            applicationId "com.mathandoro.coachplus"
            buildConfigField "String", "BASE_URL", '"https://coach.plus"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:transition:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.theartofdev.edmodo:android-image-cropper:2.7.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation 'io.reactivex.rxjava2:rxjava:2.x.x'

    testImplementation 'junit:junit:4.12'
}
